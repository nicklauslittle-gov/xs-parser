plugins {
	id 'checkstyle'
	id 'jacoco'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'net.sf.saxon:Saxon-HE:10.+'
	testImplementation 'junit:junit:4.13'
	testRuntimeOnly 'net.sf.saxon:Saxon-HE:10.+'
}

java {
	withSourcesJar()
	withJavadocJar()
}

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

def getVersion = { ->
	def stdout = new ByteArrayOutputStream()
	def result = exec {
		commandLine 'git', 'describe', '--tags', '--match', 'v[0-9]*'
		standardOutput = stdout
		ignoreExitValue true
	}
	if (result.getExitValue() != 0) {
		return 'unspecified'
	}
	return stdout.toString().trim().substring(1)
}

// Run with -Psnapshot to enable SNAPSHOT builds
def isSnapshot = project.hasProperty('snapshot')
version = getVersion() + (isSnapshot ? '-SNAPSHOT' : '')

test {
	finalizedBy jacocoTestReport
	maxHeapSize = '2048m'
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	systemProperty 'XS_PARSER_VERBOSE', true
	testLogging {
		exceptionFormat 'full'
	}
}

javadoc {
	options.encoding = 'UTF-8'
	options.memberLevel = JavadocMemberLevel.PUBLIC
}

jar {
	manifest {
		attributes 'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Sealed': true
	}
}

tasks.withType(Jar) {
	from('LICENSE.md') {
		into 'META-INF'
	}
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

def getDescription = { ->
	def desc = file('README.md').text.substring('# Description'.length()).trim().replace('\r\n', '\n').replace('\r', '\n')
	return desc.substring(0, desc.indexOf('#')).trim().replaceAll('\n+', ' ').replace('`', '')
}

publishing {
	repositories {
		maven {
			url = { isSnapshot ? snapshotRepositoryUrl : releaseRepositoryUrl }
			credentials {
				username = System.getenv('SONATYPE_USERNAME')
				password = System.getenv('SONATYPE_PASSWORD')
			}
		}
	}
	publications {
		sonatype(MavenPublication) {
			artifactId = artifact
			from components.java
			pom {
				def repoUrl = System.getenv('GITHUB_SERVER_URL') + '/' + System.getenv('GITHUB_REPOSITORY')
				def baseRepoUrl = repoUrl.substring('https://'.length())
				name = rootProject.name
				description = getDescription()
				url = repoUrl
				organization {
					name = group
					url = repoUrl.substring(0, repoUrl.lastIndexOf('/'))
				}
				issueManagement {
					system = 'GitHub'
					url = "${repoUrl}/issues"
				}
				licenses {
					license {
						name = file('LICENSE.md').text.split('\n', 2)[0].trim()
						url = "${repoUrl}/blob/main/LICENSE.md"
					}
				}
				scm {
					connection = "scm:git:git://${baseRepoUrl}.git"
					developerConnection = "scm:git:ssh://${baseRepoUrl}.git"
					url = repoUrl
				}
				withXml {
					def devs = asNode().appendNode('developers')
					def authors = (file('CONTRIBUTORS.md').text =~ /- Copyright [0-9]{4}(-([0-9]{4}|present))? (.*?) <(.*?)>/).findAll()
					authors.each {
						def dev = devs.appendNode('developer')
						dev.appendNode('name', it[3])
						dev.appendNode('email', it[4])
					}
				}
			}
		}
	}
}

signing {
	required = { gradle.taskGraph.hasTask('uploadArchives') }
	def signingKey = System.getenv('PGP_SIGNING_KEY')
	def signingPassword = System.getenv('PGP_SIGNING_PASSWORD')
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications
}
